generate-cursor-symlink path:
    #!/bin/bash
    echo "Finding git projects under {{path}}..."
    find "{{path}}" -type d -name ".git" -exec dirname {} \; | while read gitdir; do \
        echo "Processing git project: $gitdir"; \
        (cd "$gitdir" && "{{justfile_directory()}}/scripts/create_cursor_symlinks.sh") || echo "  ‚ùå Error in $gitdir"; \
    done

build-rules: build-rules-cursor build-rules-windsurf build-rules-claude

build-rules-cursor:
    mkdir -p build/cursor
    for file in rules/always-on/*.md; do \
        name="$(basename "$file" .md)"; \
        DESCRIPTION="General engineering rules for Cursor" ALWAYS_APPLY=true \
          gomplate --file=rules/templates/cursor.mdc.t \
          --out=build/cursor/"${name}".mdc \
          --template body="$file"; \
    done
    for file in rules/on-demand/*.md; do \
        name="$(basename "$file" .md)"; \
        DESCRIPTION="One-shot workflow rules for Cursor" ALWAYS_APPLY=false \
          gomplate --file=rules/templates/cursor.mdc.t \
          --out=build/cursor/"${name}".mdc \
          --template body="$file"; \
    done

build-rules-windsurf:
    mkdir -p build/windsurf
    for file in rules/always-on/*.md; do \
        name="$(basename "$file" .md)"; \
        DESCRIPTION="General engineering rules for Windsurf" TRIGGER="always_on" ALWAYS_APPLY=true \
          gomplate --file=rules/templates/windsurf.mdc.t \
          --out=build/windsurf/"${name}".mdc \
          --template body="$file"; \
    done
    for file in rules/on-demand/*.md; do \
        name="$(basename "$file" .md)"; \
        DESCRIPTION="One-shot workflow rules for Windsurf" TRIGGER="manual" ALWAYS_APPLY=false \
          gomplate --file=rules/templates/windsurf.mdc.t \
          --out=build/windsurf/"${name}".mdc \
          --template body="$file"; \
    done

build-rules-claude:
    mkdir -p build/claude
    gomplate --file=rules/templates/claude.md.t \
      --out=build/claude/claude.md \
      --template body=rules/always-on/engineering.md

